module alu (
    input  wire [31:0] a,
    input  wire [31:0] b,
    input  wire [3:0]  alu_sel,

    output reg  [31:0] result,
    output wire        zero
);

    always @(*) begin

        case (alu_sel)

            // ADD
            4'b0000:
                result = a + b;

            // SUB
            4'b0001:
                result = a - b;

            // AND
            4'b0010:
                result = a & b;

            // OR
            4'b0011:
                result = a | b;

            // XOR
            4'b0100:
                result = a ^ b;

            // Signed comparison
            4'b0101:
                result = ($signed(a) < $signed(b)) ? 32'd1 : 32'd0;

            // Unsigned comparison
            4'b0110:
                result = (a < b) ? 32'd1 : 32'd0;

            // Logical left shift
            4'b0111:
                result = a << b[4:0];

            // Logical right shift
            4'b1000:
                result = a >> b[4:0];

            // Arithmetic right shift
            4'b1001:
                result = $signed(a) >>> b[4:0];

            default:
                result = 32'd0;

        endcase

    end

    assign zero = (result == 32'd0);

endmodule
